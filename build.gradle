plugins {
    id 'java-library'
    id 'maven'
    id 'signing'
}

group = 'com.ragnaroh'
archivesBaseName = 'assertj-json'
version = '0.2.0-SNAPSHOT'

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/') {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'AssertJ JSON'
                packaging 'jar'
                artifactId 'assertj-json'
                description 'Java library for asserting JSON using AssertJ and Gson' 
                url 'https://github.com/ragnaroh/assertj-json'
           
                scm {
                    connection 'scm:git:git://github.com/ragnaroh/assertj-json.git'
                    developerConnection 'scm:git:ssh://github.com/ragnaroh/assertj-json.git'
                    url 'https://github.com/ragnaroh/assertj-json'
                }
           
                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
           
                developers {
                    developer {
                        id 'ragnaroh'
                        name 'Ragnar Haugan'
                        email 'ragnaroh@gmail.com'
                    }
                }
            }
        }
    }
}

signing {
    sign configurations.archives
}

repositories {
    jcenter()
}

dependencies {
    api 'org.assertj:assertj-core:3.14.0'
    api 'com.google.code.gson:gson:2.8.6'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
}

compileJava {
    sourceCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 13
    options.compilerArgs.add('--enable-preview')
}

test {
    useJUnitPlatform()
    jvmArgs(['--enable-preview'])
}
